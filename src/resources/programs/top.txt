//Function definitions

// VEC: The NELEM long vector that ADFG should be applied to. Should be either DV or DC
func applyDensityFilterGradient(VEC) = {
    //For each element, calculate unityscale and scale the central element by this value. Store that value in tmp
    //Unityscale is obtained by getting all neighbours, dividing by themselves to get 1, and then multiplying
    //neighbours with their known, constant filterweight. Summing all filterweights gives unityscale
    pstart double //Initial setup
    estart
    mul.sx x0, s0, x0 //clear x0
    add.ix x0, x0, 1 //Set x0=1
    eend
    pend

    pstart nelemstep //Step through all elements
    ld.sel x1, VEC //x1 = v[e1]
    ld.fcn x2, VEC //x2 = fcn[e1]
    ld.edn1 x3, VEC //x3 = edn1[e1]
    ld.edn2 x4, VEC //x4 = edn2[e1]
    estart
    div.xx x2, x2, x2 //x2 = x2/x2 (all non-zero elements become 1, zeros stay zero)
    div.xx x3, x3, x3 //x3 = x3/x3
    div.xx x4, x4, x4 //x4 = ones
    mul.ix x2, x2, 0.5 //face neighbour weight = 0.5
    mul.ix x3, x3, 0.08578 //Edge neighbour weight = 0.08578
    mul.ix x4, x4, 0.08578 //edge neighbour weight = 0.08578
    red.xx s1, x0, x2 //Sum all face neighbour weightings
    red.xx s2, x0, x3 //Sum some edge neigbour weights
    add.ss s1, s1, s2 //Add partial sum
    red.xx s2, x0, x4 //Sum remaining edge neighbour weights
    add.ss s1, s1, s2 //Add remaining edge neighbours
    add.is s1, s1, 1.5 //Add central element weighting. s1 = unityscale
    div.is s1, s1, 1 //s1 = 1/unityscale
    mul.sx x1, s1, x1 //x1 = v[e1]/unityscale
    eend
    st.sel x1, TMP
    pend

    //For each element in DV, clear its value. Then loop through neighbourhood, setting new element
    //value to be a scaled version of all input values in tmp
    pstart nelemstep //Through all elements
    ld.sel x1, TMP
    ld.fcn x2, TMP
    ld.edn1 x3, TMP
    ld.edn2 x4, TMP
    estart //We need both the original values AND count of values
    mul.ix x2, x2, 0.5 //scale face neighbours
    mul.ix x3, x3, 0.08578 //Scale edge neighbours
    mul.ix x4, x4, 0.08578 //Scale edge neighbours
    mul.ix x1, x1, 1.5 //Scale central element
    red.xx s1, x1, x0 //Sum values in x1
    red.xx s2, x2, x0 //Sum values in x2
    add.ss s1, s1, s2
    red.xx s2, x3, x0 //Sum values in x3
    add.ss s1, s1, s2
    red.xx s2, x4, x0 //Sum values in x4
    add.ss s1, s1, s2 //s1 = v[e1] += filterWeight*tmp[e2] for all neighbours
    div.xx x1, x1, x1 //Reset value in x1 to a single value
    mul.sx x1, s1, x1 //Set first value in x1 to new element value
    eend
    st.sel x1, VEC
    pend
}

//DENS: The vector containing densities. Either X or XPHYS
//INPUT: The input vector being used: Either U, ??
//OUTPUT: The output vector: Either R,
func applyStateOperator(DENS, INPUT, OUTPUT) = {

    pstart double //Setu up e0, emin constants. Being setup twice, but that's OK
    estart
    add.is s1, s0, 0.0078125
    mul.ss s1, s1, s1 //s1 = 6.1e-5
    add.is s2, s0, 0.015625
    mul.ss s2, s1, s2 //s2 = 9.5e-7 ~= 1e-6 = emin
    add.is s1, s0, 1 //s1 = 1 = e0
    sub.ss s1, s1, s2 //s1 = e0-emin
    mul.sv v0, s0, v0 //clear v0
    eend
    pend

    //Clear output vector
    pstart ndof
    estart
    eend
    st.vec v0, OUTPUT
    pend

    pstart nelemdof //Loop over elements and perform applyStateOperator
    ld.dof v0, INPUT
    ld.dof v1, OUTPUT
    ld.elem x0, DENS
    estart
    mul.xx x1, x0, x0 //x0 = pow(x,2)
    mul.xx x1, x1, x0 //x1 = pow(x,3)
    mul.sx x1, s1, x1 //x1 = pow(x,3)*(e0-emin)
    add.sx x1, s2, x1 //x1 = emin+pow(x,3)*(e0-emin)
    mul.xv v2, x1, v0 //v2 = u_local*elementScale
    mac.kv v2, v2     //ke*(u_local*elementScale)
    add.vv v2, v2, v1 //out[edof[i]] += out_local[i]
    eend
    st.dof v2, OUTPUT
    st.fdof v0, OUTPUT //Store fixed DOFs from inp. values
    pend
}

//Constantly used registers
//s15: Loop
//s14: change

//This is where all the setup in top3dcg is performed
//populate design space
//Volfrac = 0.2
//tstart clear
pstart nelemvec
estart
mul.sv v0, s0, v0 //clear v0
mul.sv v1, s0, v1 //Clear v1
add.iv v0, v0, 0.2 //v0 = 0.2
add.iv v1, v1, 1 //v1 = 1
eend
st.vec v0, X
st.vec v0, XPHYS
st.vec v1, DV
pend

applyDensityFilterGradient(DV)

//Set up loop and change variables
pstart single
estart
add.ss s15, s0, s0 //s15 =loop =0
add.is s14, s0, 1  //s14=change=1
eend
pend

//Main loop start
TOP3DCG:
pstart single
estart
add.is s15, s15, 1 //loop++
eend
pend

//Solve state conjugate gradient

//Set up and clear a lot of vectors
pstart double
estart
mul.sv v0, s0, v0 //clear v0 of both threads
eend
pend

pstart ndof //Clear all ndof long vectors except U,F
estart
eend
st.vec v0, R
st.vec v0, Z
st.vec v0, P
st.vec v0, Q
st.vec v0, INVD
st.vec v0, TMP
pend

applyStateOperator(XPHYS, U, R)